load "iovtk"
load "pipe"
load "MUMPS_seq"
include "gnuplotInterface.edp"

include "Parameters.edp"

/*  Domain definition and mesh generation   *\
\* for filtration and backstress definition */

int NN = 30;

int labelB = 1; /* Bottom */
int labelR = 2; /* Right hand side of domain */
int labelT = 3; /* Top of domain */
int labelL = 4; /* Left hand side of domain */

real[int] xP = [0.0, L, L, 0.0]; /* enumeration of xcoords */
real[int] yP = [0.0, 0.0, H, H]; /* enumeration of ycoords */

int[int] nGamma = [10*1, H/L, L/L, 10*H/L] * NN; /* number of mesh parts on fracture, on the rest of bottom, etc. */
int[int] labelGamma = [labelB, labelR, labelT, labelL];

border GammaDomain(t = 0.0, 1.0; i)    /*i - number of boundary(label), t - parameter*/
{
    int iNext = (i+1) % nGamma.n;   /* .n - the length of the nGamma massive*/

    x = xP[i]*(1-t) + xP[iNext]*t;
    y = yP[i]*(1-t) + yP[iNext]*t;
    label = labelGamma[i]; /* the flag of the boundary */
}

mesh Th = buildmesh(GammaDomain(nGamma));
plot(Th);
exit(0);

/*   Domain definition and mesh generation  *\
\*      for fracture width definition       */

real frcS = 20.0;
int NNN = 100;

real xMin = layerBorder[0];
real xFracL = layerBorder[2] - frcS;
real xFracR1 = layerBorder[3];
real xFracR2 = layerBorder[4];
real xFracR = layerBorder[4] + frcS;
real xMax = layerBorder[5];

real yMax = (xMin + xMax) / 2.0 / 2.0;
real Hsize = xMax - xMin;

real[int] xPw = [xMin, xFracL, xFracR1, xFracR2, xFracR, xMax, xMax, xMin];
real[int] yPw = [0, 0, 0, 0, 0, 0, yMax, yMax];

int[int] nGammaW = [(xFracL - xMin)/Hsize, 
                   20*(xFracR1 - xFracL)/Hsize,
                   20*(xFracR2 - xFracR1)/Hsize, 
                   20*(xFracR - xFracR2)/Hsize,
                   (xMax - xFracR)/Hsize,
                   yMax/Hsize, 1, yMax/Hsize] * NNN;

int labelBottom = 1;
int labelFrac1 = 2;
int labelFrac2 = 3;
int labelFrac3 = 4;
int labelRight = 5;
int labelTop = 6;
int labelLeft = 7;
int[int] labelGammaW = [labelBottom, labelFrac1, labelFrac2, labelFrac3, labelBottom,
                       labelRight, labelTop, labelLeft];

border GammaDomainW( t = 0.0, 1.0; i ) {
    int iNext = (i + 1)%nGammaW.n;
    x = xPw[i]*(1.0 - t) + xPw[iNext]*t;
    y = yPw[i]*(1.0 - t) + yPw[iNext]*t;
    label = labelGammaW[i];
}

mesh Thw = buildmesh(GammaDomainW(nGammaW));

/*  Fespace definition for   *\
\* filtration and backstress */

fespace Vh(Th, P2);
Vh p, phi;
Vh pOld;
Vh RHSQ;
pOld = p0;

Vh psi, khi;
Vh RHS = alpha*(p-p0)/PP;
Vh psix, psiy, psixx, psiyy, psixy, psiyx;
Vh deltaSx, deltaSxTot, deltaSy, deltaSyTot, deltaSxy;
Vh sBfull = 0.0;

/*  Fespace definition for  *\
\*    elasticity problem    */

fespace Wh(Thw, [P2, P2]);
fespace Xh(Thw, P1);

Wh [u, v];
Wh [uIter, vIter];
Wh [phiw, psiw];
// Xh Sxx;
Xh Syy;

Xh pf = 26.4*1e6;
Xh pEff = 0.0;
Xh pNet = 0.0;

Xh PrFunc = pr[0] * (x <= layerBorder[1]) + pr[1] * (x > layerBorder[1] && x <= layerBorder[2]) 
         + pr[2] * (x > layerBorder[2] && x <= layerBorder[3])+ pr[3] * (x > layerBorder[3] && x <= layerBorder[4]) 
         + pr[4] * (x > layerBorder[4]);
Xh EFunc = Ew[0] * (x <= layerBorder[1]) + Ew[1] * (x > layerBorder[1] && x <= layerBorder[2])
           + Ew[2] * (x > layerBorder[2] && x <= layerBorder[3]) + Ew[3] * (x > layerBorder[3] && x <= layerBorder[4])
           + Ew[4] * (x > layerBorder[4]);
Xh nuFunc = nuw[0] * (x <= layerBorder[1]) + nuw[1] * (x > layerBorder[1] && x <= layerBorder[2])
            + nuw[2] * (x > layerBorder[2] && x <= layerBorder[3]) + nuw[3] * (x > layerBorder[3] && x <= layerBorder[4])
            + nuw[4] * (x > layerBorder[4]);
Xh sBFunc = sB[0] * (x <= layerBorder[1]) + sB[1] * (x > layerBorder[1] && x <= layerBorder[2]) 
            + sB[2] * (x > layerBorder[2] && x <= layerBorder[3]) + sB[3] * (x > layerBorder[3] && x <= layerBorder[4]) 
            + sB[4] * (x > layerBorder[4]);
Xh lambdaFunc = nuFunc*EFunc / ((1 + nuFunc) * (1 - 2*nuFunc));
Xh muFunc = EFunc / (2 + 2*nuFunc);
Xh sigmaVeff = rho0*g*x - alpha*PrFunc;
Xh sigmaMin = (nuFunc / (1 - nuFunc)) * (sigmaVeff) + alpha*PrFunc;

macro grad(p)[dx(p), dy(p)] // EOM

/*  Weak problem formulation for   *\
\*    filtration and backstress    */

real[int] xdelta = [0.0]; // coord x of point source
real[int] ydelta = [0.0]; // coord y of point source
real[int] cdelta=[dt*Q];  // value of point source
matrix D = interpolate(Vh, xdelta, ydelta); // the interpolation matrix
RHSQ[] = D*cdelta;

varf FiltrationBilinearForm(p, phi) = 
    int2d(Th)(Seps*p*phi) 
    + int2d(Th)((dt*kr/mu)*grad(p)'*grad(phi)) 
    + int1d(Th, labelB)((dt*kf/mu)*dx(p)*dx(phi)*(x <= Lfrac))
    + on(labelT, p = p0)
    + on(labelR, p = p0);

varf FiltrationLinearForm(p, phi) = 
    int2d(Th)(Seps*pOld*phi)
    + RHSQ[]
    + on(labelT, p = p0)
    + on(labelR, p = p0);

varf BackstressBilinearForm(psi, khi) = 
    int2d(Th)(grad(psi)'*grad(khi))
    + on(labelR, psi=0)
    + on(labelT, psi=0);

varf BackstressLinearForm(psi, khi)=
    - int2d(Th)(RHS*khi)
    + on(labelR, psi=0)
    + on(labelT, psi=0);

/*  Weak problem formulation for   *\
\*    fracture width definition    */

real sqrt2 = sqrt(2.0);
macro div(u, v) (dx(u) + dy(v)) // EOM
macro epsilon(u, v) [dx(u), dy(v), (dy(u) + dx(v))/sqrt2] // EOM

varf ElasticityBilinearForm([u, v], [phiw, psiw]) = 
    int2d(Thw)(
        lambdaFunc*div(u,v)*div(phiw, psiw) + 2.0*muFunc*epsilon(u, v)'*epsilon(phiw, psiw)
    )
    + on(labelBottom, v = 0.0)
    + on(labelRight, u = 0.0)
    + on(labelTop, v = 0.0);

varf PenaltyBilinearForm([u, v], [phiw, psiw]) =
    int1d(Thw, labelFrac2)(
        penalty*v*(vIter < 0.0)*psiw
    )
    + int1d(Thw, labelFrac1)(
        penalty*v*(vIter < 0.0)*psiw
    )
    + int1d(Thw, labelFrac3)(
        penalty*v*(vIter < 0.0)*psiw
    )
    + on(labelBottom, v = 0.0)
    + on(labelRight, u = 0.0)
    + on(labelTop, v = 0.0);

varf RhsLinearForm([u, v], [phiw, psiw]) = 
    int2d(Thw)(
        rho0*g*phiw
    )
    + int1d(Thw, labelFrac2)(
        pEff*psiw
    )
    + int1d(Thw, labelFrac1)(
        pEff*(vIter > 0.0)*psiw
    )
    + int1d(Thw, labelFrac3)(
        pEff*(vIter > 0.0)*psiw
    )
    + int1d(Thw, labelLeft)(
        sigmaVeff*phiw
    )
    + on(labelBottom, v = 0.0)
    + on(labelRight, u = 0.0)
    + on(labelTop, v = 0.0);


/*---------------------------------- MAIN CYCLE ----------------------------------*/

matrix Ae = ElasticityBilinearForm(Wh, Wh);
matrix BB = BackstressBilinearForm(Vh, Vh);

real[int] omega = [1.0, 0.75, 0.5, 0.3];
int nOmega = omega.n;
int[int] order = [1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1];
real pNetAverage = 0;

// int nPoints = int(tMax/dt);
// real[int] tt(nPoints);
// real[int] bs(nPoints);
// real[int] FracPressure(nPoints);

int i = 0;
real t = 0;
while(t < tMax){
    matrix A = FiltrationBilinearForm(Vh, Vh);
    set(A, solver = sparsesolver);
    real[int] b = FiltrationLinearForm(0, Vh);
    p[] = A^-1*b;

    RHS = alpha*(p-pOld)/PP;
    real[int] bB = BackstressLinearForm(0, Vh);
    psi[] = BB^-1*bB;
    psix = dx(psi);
    psiy = dy(psi);
    psixx = dx(psix);
    psiyy = dy(psiy);
    // deltaSx = lambda*(psixx+psiyy)+2*G*psixx;
    // deltaSxTot = deltaSx-alpha*(p-pOld);
    deltaSy = lambda*(psixx+psiyy)+2*G*psiyy;
    deltaSyTot = deltaSy-alpha*(p-pOld);
    sBfull = sBfull + deltaSyTot;
    real bsAverage = -int1d(Th, labelB)(sBfull*(x <= Lfrac))/Lfrac;
    sB[3] = bsAverage;

    pf = int1d(Th, labelB)(p*(x <= Lfrac))/Lfrac;

    
    sBFunc = sB[0] * (x <= layerBorder[1]) + sB[1] * (x > layerBorder[1] && x <= layerBorder[2]) 
              + sB[2] * (x > layerBorder[2] && x <= layerBorder[3]) + sB[3] * (x > layerBorder[3] && x <= layerBorder[4]) 
              + sB[4] * (x > layerBorder[4]);
    pEff = pf - alpha*PrFunc - sBFunc;
    pNet = pf - sigmaMin - sBFunc;
    pNetAverage = int1d(Thw, labelFrac2)(pNet) / (layerBorder[4] - layerBorder[3]);
    if(pNetAverage >= 2*KIc/sqrt(pi*(layerBorder[4] - layerBorder[3]))){
        Lfrac += 1;
    }

    real absErrorL2 = 1.0;
    real normL2 = 1.0;
    real relErrorL2 = 1.0;
    int count = 0;
    int iOmega = 0;
    [uIter, vIter] = [0.0, 0.0];

    cout << endl;
    cout << "Time = " << t << " s" << endl;

    while (relErrorL2 > tol && count < maxIter) {
        matrix Ap = PenaltyBilinearForm(Wh, Wh);
        matrix Aw = Ae + Ap;
        real[int] bw = RhsLinearForm(0, Wh);
        set(Aw, solver=sparsesolver);
        u[] = Aw^-1*bw;
        
        normL2 = sqrt(int2d(Thw)(v^2));
        absErrorL2 = sqrt(int2d(Thw)((v - vIter)^2));
        relErrorL2 = absErrorL2 / normL2;

        uIter[] = omega[iOmega]*u[] + (1.0 - omega[iOmega])*uIter[];
        ++count;

        if (count == maxIter && relErrorL2 > tol && iOmega + 1 < nOmega) {
            count = 0;
            iOmega += 1;  
        }

        cout << "    relaxation parameter = " << omega[iOmega] << " (" << iOmega + 1 << " / " << nOmega << ")" << endl;
        cout << "    iteration = " << count << " (" << maxIter << ")" << endl;
        cout << "    rel_error_L2 = " << relErrorL2 << " (" << tol << ")" << endl;
    }

    // Sxx = lambdaFunc*div(u, v) + 2.0*muFunc*dx(u);
    Syy = lambdaFunc*div(u, v) + 2.0*muFunc*dy(v);

    savevtk("./Results/NewResults" + i + ".vtu", Thw, u, v, Syy, -sigmaMin, pNet, pEff, dataname = "u v Syy Smin pNet pEff", order=order);
    savevtk("./Results/FiltrationResults" + i + ".vtu", Th, p, sBfull, psi, dataname = "p Backstress psi ", order=order);

    pOld = p;
    t = t + dt;
    i = i + 1;
}

// string[int] legend = ["Backstress", "Backstress"];
// string[int] legenD = ["FracPressure", ""];
// string sgnuplot = "gnuplot -persist";
// pstream pipeStream(sgnuplot, "w");
// drawTwoCurvesGnuplot(pipeStream, tt, bs, bs, legend);
// drawTwoCurvesGnuplot(pipeStream, tt, FracPressure, FracPressure, legenD);
